abstract Attempto =
  Numeral, Symbols ** {

-- Use Text to get only single-sentence texts.
flags startcat = ACEText ;

cat CN ; cat VarCN ;
cat NP ; cat ThereNP ;
cat Card ;
---cat Numeral ;
cat PN ;
cat A ;
cat A2 ;
cat AP ;
cat RS ;
cat Pron ; IndefPron ; IndefTherePron ;
cat Prep ;
cat S ; cat SimpleS ;
cat VP ;
cat V ;
cat VS ;
cat V2 ;
cat V3 ;
cat Adv ;
cat Conj ;
cat IP ;
cat IAdv ;
cat IDet ;
cat QS ;
cat Text ;
cat RP ;
cat Unit ;
cat MCN ;
cat PP ;
cat VPS ;
cat [VPS] {2} ;


fun aNP : VarCN -> ThereNP ;
fun theNP : VarCN -> NP ;
-- Note: in full ACE this must be ThereNP [KK]
fun noNP : VarCN -> NP ;
fun everyNP : VarCN -> NP ;

fun pnNP : PN -> NP ;

-- [JJC] [KK]
-- - ACE and AceWiki do not allow 'there is every-'
-- - AceWiki does not allow 'there is no-' (but ACE does)
-- - ACE and AceWiki allow 'there is some-'
fun somebody_IPron : IndefTherePron ;
fun something_IPron : IndefTherePron ;
fun everybody_IPron : IndefPron ;
fun everything_IPron : IndefPron ;
fun nobody_IPron : IndefPron ;
fun nothing_IPron : IndefPron ;

-- [JJC] [KK]
fun indefTherePronNP : IndefTherePron -> ThereNP ;
fun indefPronNP : IndefPron -> NP ;

fun indefTherePronVarNP : IndefTherePron -> Var -> ThereNP ;
fun indefPronVarNP : IndefPron -> Var -> NP ;

fun at_leastNP : Card -> VarCN -> ThereNP ;
fun at_mostNP : Card -> VarCN -> ThereNP ;
fun more_thanNP : Card -> VarCN -> ThereNP ;
fun less_thanNP : Card -> VarCN -> ThereNP ;
fun exactlyNP : Card -> VarCN -> ThereNP ;

fun nothing_butNP : VarCN -> NP ;

fun apposVarCN : CN -> Var -> VarCN ;  -- a man X

fun cn_as_VarCN : CN -> VarCN ;

-- Note: in full ACE this must be ThereNP [KK]
fun termNP : Term -> NP ;

-- 2.2.2

-- Relative clause can be attached to both CNs and NPs.
-- relCN provides relative clauses in constructs like
-- `which woman who ...` and `for every woman who ...`
fun relCN : VarCN -> RS -> VarCN ;
fun relNP : NP -> RS -> NP ;
--fun relThereNP : ThereNP -> RS -> ThereNP ; avoid ambiguity generated by ThereNP [JJC]

fun andRS : RS -> RS -> RS ;
fun orRS : RS -> RS -> RS ;

fun predRS : RP -> VP -> RS ;
fun neg_predRS : RP -> VP -> RS ;
fun slashRS : RP -> NP -> V2 -> RS ;
fun neg_slashRS : RP -> NP -> V2 -> RS ;

fun which_RP : RP ;

-- 2.2.4

fun ofCN : VarCN -> NP -> VarCN ; -- dog of John and Mary

-- 2.3.1

fun vpS : NP -> VP -> SimpleS ;
fun neg_vpS : NP -> VP -> SimpleS ;

fun v2VP : V2 -> NP -> VP ;

-- 2.3.2 [JJC]

fun a2VP : A2 -> NP -> VP ; -- is mad-about NP

-- 3.2

fun thereNP : ThereNP -> SimpleS ;  -- there is/are

fun thereNP_as_NP : ThereNP -> NP ;

-- 3.4.1

fun coordS : Conj -> S -> S -> S ;

fun and_Conj : Conj ;
fun or_Conj : Conj ;

-- In order to implement VP coordination, we
-- (1) convert VP into VPS,
-- (2) use VPS coordination producing VPS,
-- (3) map VPS into S (and QS?) (but definitely not into RS).
fun vp_as_posVPS : VP -> VPS ;
fun vp_as_negVPS : VP -> VPS ;
fun np_coord_VPS : NP -> Conj -> [VPS] -> SimpleS ;

-- 3.4.3

fun for_everyS : VarCN -> S -> S ;

-- 3.4.4

fun if_thenS : S -> S -> S ;
fun falseS : SimpleS -> S ; -- it is false that

fun simpleS_as_S : SimpleS -> S ;

-- 3.5

-- These have been replaced by the more generic function npqQS, which allows
-- for wh-words in the object position of a relative clause as subject
-- e.g. "somebody who is who is a man?"
--fun ipQS : IP -> VP -> QS ;
--fun neg_ipQS : IP -> VP -> QS ;

-- who does Mary like?
fun slash_ipQS : IP -> NP -> V2 -> QS ;

-- who does Mary not like?
fun neg_slash_ipQS : IP -> NP -> V2 -> QS ;

fun whoSg_IP : IP ;
fun whatSg_IP : IP ;

fun whichIP : IDet -> VarCN -> IP ;
fun which_IDet : IDet ;
fun whichPl_IDet : IDet ;

-- 4

cat ACEText ;
fun consText : Text -> ACEText -> ACEText ;

fun baseText : Text -> ACEText ;

fun sText : S -> Text ;
fun qsText : QS -> Text ;

-- more

fun npVP  : NP -> VP ;              -- is a bank
fun digitsCard : Digits -> Card ;   -- 8 banks
fun v2_byVP : V2 -> NP -> VP ;      -- is bought by a customer


-- ===== Question Stuff to be moved (ugh) =====

-- Categories
cat VarCNQ ; -- question CN, e.g. "friend of who" ?
cat NPQ ; -- question NP, e.g. "a friend of who" ?
cat VPQ ; -- question VP, e.g. "is who" ?
cat RSQ ; -- question RS, e.g. somebody "who is who" ?

-- Syntax
fun vpqQS : NP -> VPQ -> QS ; -- "Mary is who ?"
fun neg_vpqQS : NP -> VPQ -> QS ; -- "Mary isn't who ?"

-- These replace the ipQS functions, i.e. "who is a man ?"
fun npqQS : NPQ -> VP -> QS ; -- "somebody who is who is a man ?"
fun neg_npqQS : NPQ -> VP -> QS ; -- "somebody who isn't who is a man ?"

-- VPQ, VPS
fun npqVPQ : NPQ -> VPQ ; -- "is who"
fun v2VPQ : V2 -> NPQ -> VPQ ; -- "likes who"
fun a2VPQ : A2 -> NPQ -> VPQ ; -- "is mad-about who"
fun v2_byVPQ : V2 -> NPQ -> VPQ ; -- "is asked by who"

-- Co-ordination
cat VPSQ ; [VPSQ] {2} ; -- a VPS which contains at least one VPQ (ie the sentence should be a question)
fun vp_as_posVPSQ, vp_as_negVPSQ : VP -> VPSQ ; -- this allows heterogenous lists of VP/VPQ's
fun vpq_as_posVPSQ, vpq_as_negVPSQ : VPQ -> VPSQ ;
fun np_coord_VPSQ : NP -> Conj -> [VPSQ] -> QS ;
fun npq_coord_VPS : NPQ -> Conj -> [VPS] -> QS ;

-- VarCNQ, NPQ
fun ofnpqCN : VarCN -> NPQ -> VarCNQ ; -- "friend of who" ?
fun aNPQ : VarCNQ -> NPQ ;
fun theNPQ : VarCNQ -> NPQ ;
fun noNPQ : VarCNQ -> NPQ ;
fun everyNPQ : VarCNQ -> NPQ ;

fun ipNPQ : IP -> NPQ ; -- "who"
fun relNPQ : NP -> RSQ -> NPQ ; -- "somebody who is a friend of who" ?

-- RSQ
fun predRSQ : RP -> VPQ -> RSQ ; -- "who is who" ?
fun neg_predRSQ : RP -> VPQ -> RSQ ; -- "who isn't who" ?
fun slashRSQ : RP -> NPQ -> V2 -> RSQ ; -- "who who asks" ?
fun neg_slashRSQ : RP -> NPQ -> V2 -> RSQ ; -- "who who doesn't ask"?


}
